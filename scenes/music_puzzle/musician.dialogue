~ start
if puzzle.is_solved():
	=> well_done

if not first_conversation:
	=> hello_again

set first_conversation = false

Musician: Hello there, Storyweaver.
if puzzle.get_progress() >= 1:
	Musician: Oh! Have you been playing with the stones over there?
	Player: I kicked them and something strange happened...
else:
	Musician: Have you seen those stones all over this island?
	- What about them? They're just inert lumps of rock.
		Musician: Oh, I wouldn't be so sure about that!
	- There's something unusual about themâ€¦ it's like they have a certain energy.
	Musician: They're tuned to a major scale!
Musician: Legend has it that playing the right melody will open a door.
=> demonstrate_melody

~ hello_again
Musician: Hello again, Storyweaver. Would you like me to remind you what I said?
- Yes => demonstrate_melody
- No
	Musician: OK then, good luck.
	=> END

~ demonstrate_melody
Musician: My mother taught me the first line of the melody. I think I can remember it. Let's see...
Musician: do,[do play("C")] re,[do play("D")] mi,[do play("E")] so,[do play("G")] ...
Musician: But she never knew the rest of the melody, only that it was somehow related to the first line.
Musician: There are some strange markings near the rocks. I've never been able to understand them though. Can you help us to find the rest of the melody?
=> END

~ well_done
Musician: Well done, Storyweaver!
=> END
