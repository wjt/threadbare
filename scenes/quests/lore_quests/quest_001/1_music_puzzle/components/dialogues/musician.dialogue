# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
do puzzle.stop_hints()
if puzzle.is_solved():
	=> item_hint

if not first_conversation:
	=> hello_again

set first_conversation = false

Musician: Ah! Another wanderer. Welcome to the Song Sanctuary. Or what's left of it.
StoryWeaver: What happened here?
Musician: Songs stopped being sung. And when a song's forgotten... the world forgets itself right along with it.
if puzzle.get_progress() >= 1:
	Musician: Oh! Have you been playing with the stones over there?
	StoryWeaver: I kicked them and something strange happened...
Musician: These stones? They're part of a magical song that used to open the portal to the Ink Well, where songwriters found ink to write their songs. But...
Musician: Um... Anyway, now I can only recall the first few notes of the magical melody. The rest slips away.
StoryWeaver: Can you show me what you remember? Maybe I can help.
=> demonstrate_melody

~ hello_again
Musician: Hello again, StoryWeaver. Would you like me to play the magical melody again?
- Yes => demonstrate_melody
- No
	Musician: OK then, good luck.
	=> END
- Do you remember anything else?
	if puzzle.get_progress() == 0:
		=> hint_melody_0
	if puzzle.get_progress() == 1:
		=> hint_melody_1
	if puzzle.get_progress() == 2:
		=> hint_melody_2
	if puzzle.get_progress() == 3:
		=> hint_melody_3
	Musician: Good luck out there.
	=> END

~ demonstrate_melody
Musician: I'll try. Let's see...
Musician: do,[do play("C")] re,[do play("D")] mi,[do play("E")] sol,[do play("G")] ...
Musician: And then it fades.
if first_conversation:
	StoryWeaver: What if I tried to complete it?
	Musician: Yes, please!
Musician: I wish you luck.
=> END

~ hint_melody_0
if get_limited_hint_level() == 0:
	Musician: I can almost hear it again... those first few notes she always played.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: The melody began gently... just like the one I showed you.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: Try playing the same notes I just demonstrated. Maybe it will spark something.
	do advance_hint_level()
	=> END

~ hint_melody_1
if get_limited_hint_level() == 0:
	Musician: My grandmother once said melodies often rise... like stepping up a staircase.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: Itâ€™s like the melody lifts itself, each note a little higher than before.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: Try shifting every note upward... one step at a time.
	do advance_hint_level()
	=> END

~ hint_melody_2
if get_limited_hint_level() == 0:
	Musician: I recall the melody felt... reversed, like moving through memory backward.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: The same notes return, but in the opposite direction.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: Play the last melody in reverse. It may sound familiar, but different.
	do advance_hint_level()
	=> END

~ hint_melody_3
if get_limited_hint_level() == 0:
	Musician: There was a descent... like the melody stepped gently downward.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 1:
	Musician: I remember hearing the notes lower, still recognizable, but softer.
	do advance_hint_level()
	=> END
if get_limited_hint_level() == 2:
	Musician: Try taking each note of the last line... and moving it one step down.
	do advance_hint_level()
	=> END

~ well_done
Musician: The melody... it's not just music. It opens the portal to the Ink Well. Let's go find more ink so songs and stories can be written again.
Musician: But we must be careful, the InkDrinkers dwell there!
=> END

~ item_hint
Musician: (PLACEHOLDER) You did it! The melody worked. I left a special item near the stones, grab it before you go!
=> END
